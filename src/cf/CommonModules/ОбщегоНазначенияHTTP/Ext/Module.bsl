
#Область ПрограммныйИнтерфейс

Функция ОтветJSON(Объект, КодСостояния = 200) Экспорт
	Ответ = Новый HTTPСервисОтвет(КодСостояния);
	
	Ответ.Заголовки.Вставить("Content-Type", ТипКонтентаJSON());
	Ответ.УстановитьТелоИзСтроки(КоннекторHTTP.ОбъектВJson(Объект));
	
	Возврат Ответ;
КонецФункции

Функция ПростойУспешныйОтвет() Экспорт
	Результат = Новый Структура("result", "ok");
	
	Возврат ОтветJSON(Результат);
КонецФункции

Функция ОтветОбОшибке(ИнформацияОбОшибке) Экспорт
	ЗаписьЖурналаРегистрации(
		"HTTPСервисы.Ошибка", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке)
	);
	
	Результат = Новый Структура("result, errorText", "error", КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	
	Возврат ОтветJSON(Результат, 500);
КонецФункции

Процедура ЗаписатьЛогДляВходящегоHTTPЗапроса(ИмяМетода, Запрос, Ответ) Экспорт
	НовыйДокумент = Документы.ВходящийHTTPЗапрос.СоздатьДокумент();
	НовыйДокумент.Дата = ТекущаяДатаСеанса();
	НовыйДокумент.ИмяМетода = ИмяМетода;
	НовыйДокумент.КодСостояния = Ответ.КодСостояния;
	НовыйДокумент.ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	НовыйДокумент.ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	
	НовыйДокумент.Записать();
КонецПроцедуры

Процедура ЗаписатьЛогДляОтветаВнешнегоСервиса(ИмяМетода, Ответ) Экспорт
	НовыйДокумент = Документы.ОтветВнешнегоСервиса.СоздатьДокумент();
	НовыйДокумент.Дата = ТекущаяДатаСеанса();
	НовыйДокумент.ИмяМетода = ИмяМетода;
	НовыйДокумент.КодСостояния = Ответ.КодСостояния;
	
	ТипТелаОтвета = ТипЗнч(Ответ.Тело);
	Если ТипТелаОтвета = Тип("ДвоичныеДанные") Тогда
		НовыйДокумент.ТелоОтвета = КоннекторHTTP.КакТекст(Ответ);
	ИначеЕсли ТипТелаОтвета = Тип("Строка") Тогда
		НовыйДокумент.ТелоОтвета = Ответ.Тело;
	КонецЕсли;
	
	НовыйДокумент.Записать();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТипКонтентаJSON()
	Возврат "application/json";
КонецФункции

#КонецОбласти