
#Область ПрограммныйИнтерфейс

#Область РаботаСTelegramAPI

Функция getMe(Бот) Экспорт
	token = ТокенБота(Бот);
	
	Ресурс = СтрШаблон("https://api.telegram.org/bot%1/getMe", token);
	
	Возврат КоннекторHTTP.Get(Ресурс);
КонецФункции

Функция setWebhook(Бот, url) Экспорт
	token = ТокенБота(Бот);
	
	Ресурс = СтрШаблон("https://api.telegram.org/bot%1/setWebhook", token);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("url", url);
	
	Возврат КоннекторHTTP.Get(Ресурс, ПараметрыЗапроса);
КонецФункции

Функция getWebhookInfo(Бот) Экспорт
	token = ТокенБота(Бот);
	
	Ресурс = СтрШаблон("https://api.telegram.org/bot%1/getWebhookInfo", token);
	
	Возврат КоннекторHTTP.Get(Ресурс);
КонецФункции

Функция getUpdates(Бот, offset = Неопределено, timeout = Неопределено) Экспорт
	token = ТокенБота(Бот);
	
	Ресурс = СтрШаблон("https://api.telegram.org/bot%1/getUpdates", token);
	
	ПараметрыЗапроса = Новый Структура;
	Если offset <> Неопределено Тогда
		ПараметрыЗапроса.Вставить("offset", Формат(offset, "ЧН=; ЧГ=0"));
	КонецЕсли;
	Если timeout <> Неопределено Тогда
		ПараметрыЗапроса.Вставить("timeout", Формат(timeout, "ЧН=; ЧГ=0"));
	КонецЕсли;
	
	Возврат КоннекторHTTP.Get(Ресурс, ПараметрыЗапроса);
КонецФункции

#КонецОбласти

#Область ОбработкаВебхуков

Функция ОбработатьВходящийЗапрос(Запрос) Экспорт
	ПараметрыПреобразования = Новый Структура;
	ПараметрыПреобразования.Вставить("ПрочитатьВСоответствие", Ложь);
	
	ДанныеЗапроса = КоннекторHTTP.JsonВОбъект(Запрос.ПолучитьТелоКакСтроку(),, ПараметрыПреобразования);
	
	Если ДанныеЗапроса.Свойство("message") Тогда
		Возврат ОбработатьСообщение(ДанныеЗапроса.message);
	КонецЕсли;
	
	Возврат ОбщегоНазначенияHTTP.ПростойУспешныйОтвет();
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТокенБота(Бот)
	УстановитьПривилегированныйРежим(Истина);
	token = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Бот);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат token;
КонецФункции

Функция ОбработатьСообщение(ДанныеСообщения)
	Если ДанныеСообщения.Свойство("from") Тогда
		ЗафиксироватьОтправителя(ДанныеСообщения.from);
	КонецЕсли;
	
	ИдентификаторЧата = ДанныеСообщения.chat.id;
	
	Если ДанныеСообщения.Свойство("text") Тогда
		ТекстСообщения = ДанныеСообщения.text;
	Иначе
		ТекстСообщения = "Я ничего не получил... =(";
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("method", "sendMessage");
	ДанныеОтвета.Вставить("chat_id", ИдентификаторЧата);
	ДанныеОтвета.Вставить("text", ТекстСообщения);
	ДанныеОтвета.Вставить("reply_markup", Клавиатура());
	
	Возврат ОбщегоНазначенияHTTP.ОтветJSON(ДанныеОтвета);
КонецФункции

Процедура ЗафиксироватьОтправителя(ДанныеОтправителя)
	Идентификатор = ДанныеОтправителя.id;
	
	УчетнаяЗапись = Справочники.УчетныеЗаписиТелеграм.НайтиПоКоду(Идентификатор);
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиИмениПользователя = Новый Массив;
	Если ДанныеОтправителя.Свойство("first_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.first_name);
	КонецЕсли;
	Если ДанныеОтправителя.Свойство("last_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.last_name);
	КонецЕсли;
	Если ДанныеОтправителя.Свойство("username") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.username);
	КонецЕсли;
	ИмяПользователя = СтрСоединить(ЧастиИмениПользователя, " ");
	
	НовыйЭлемент = Справочники.УчетныеЗаписиТелеграм.СоздатьЭлемент();
	НовыйЭлемент.Код = Идентификатор;
	НовыйЭлемент.Наименование = ИмяПользователя;
	НовыйЭлемент.Записать();
КонецПроцедуры

Функция Клавиатура()
	СтрокиКлавиатуры = Новый Массив;
	КнопкиСтрокиКлавиатуры = Новый Массив;
	
	КнопкаКлавиатуры = Новый Структура("text", "=(");
	КнопкиСтрокиКлавиатуры.Добавить(КнопкаКлавиатуры);
	
	КнопкаКлавиатуры = Новый Структура("text", "=)");
	КнопкиСтрокиКлавиатуры.Добавить(КнопкаКлавиатуры);
	
	СтрокиКлавиатуры.Добавить(КнопкиСтрокиКлавиатуры);
	
	Возврат Новый Структура("keyboard", СтрокиКлавиатуры);
КонецФункции

#КонецОбласти